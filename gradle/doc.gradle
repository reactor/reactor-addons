/*
 * Copyright (c) 2011-2016 Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
configure(subprojects) { project ->
	apply plugin: "java"
	apply plugin: 'org.asciidoctor.convert'

  javadoc {
	dependsOn jar
	group = "Reactor Addons Javadoc"

	description = "Generates Javadoc API documentation."
	title = "$project.name $project.version"

	if (JavaVersion.current().isJava8Compatible()) {
	  compileTestJava.options.compilerArgs += "-parameters"
	  tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	  }
	}

	options.addStringOption('charSet', 'UTF-8')

	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = "Reactor Addons: $it.name"
	options.overview = "$rootDir/docs/src/api/overview.html"
	options.stylesheetFile = rootProject.file("docs/src/api/stylesheet.css")
	options.links(rootProject.ext.javadocLinks)

	maxMemory = "1024m"
	destinationDir = new File(buildDir, "docs/javadoc")
	source project.sourceSets.main.allJava
	doFirst {
	  classpath = files(project.sourceSets.main.compileClasspath)
	}
  }



	asciidoctor {
		sources {
			include "index.asciidoc"
		}
		outputDir file("$buildDir/asciidoc")
		backends = ['html5', 'pdf']
		logDocuments = true
		options = [
				doctype: 'book'
		]
		attributes docinfo: '',
				toc2: '',
				'compat-mode': '',
				stylesdir: "stylesheets/",
				stylesheet: 'golo.css',
				'source-highlighter': 'coderay'

		doLast {
			file("$outputDir/pdf/index.pdf").renameTo("$outputDir/pdf/reference-guide.pdf")
		}

	}

}

